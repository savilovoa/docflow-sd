
&НаСервереБезКонтекста
Функция РасчетПлановогоВремениЗакрытия(ДатаВремяНачала, Сервис, Тип, Приоритет)
	
	// По умолчанию срок реагирования 4 часа
	Результат = ДатаВремяНачала + 4 * 3600;
	Запрос = Новый Запрос;
    Запрос.Текст = "ВЫБРАТЬ
                   |	СД_СервисыЧ_СУО.Срок КАК Срок
                   |ИЗ
                   |	Справочник.СД_Сервисы.Ч_СУО КАК СД_СервисыЧ_СУО
                   |ГДЕ
                   |	СД_СервисыЧ_СУО.Приоритет = &Приоритет
                   |	И СД_СервисыЧ_СУО.Тип = &Тип
                   |	И СД_СервисыЧ_СУО.Ссылка = &Ссылка";      
	Запрос.УстановитьПараметр("Ссылка", Сервис);
	Запрос.УстановитьПараметр("Тип", Тип);
	Запрос.УстановитьПараметр("Приоритет", Приоритет);
    Данные = Запрос.Выполнить().Выбрать();
	Если Данные.Следующий() Тогда
		Результат = ДатаВремяНачала + Данные.Срок * 3600; // Часы переводим в секунды
	КонецЕсли;
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ЧСД_РасчетПлановогоВремениПриИзменении(Элемент)
	// Провести новый расчет планового времени закрытия
	ДатаВремяНачала = Объект.Дата;
	Объект.Ч_ПлановоеВремяЗакрытия = РасчетПлановогоВремениЗакрытия (ДатаВремяНачала, Объект.Сервис, Объект.Ч_ТипЗаявки, ПриоритетЗаявки);	
КонецПроцедуры

&НаКлиенте
Процедура ЧСД_СервисПриИзмененииПосле(Элемент)
	// Провести новый расчет планового времени закрытия
	ЧСД_РасчетПлановогоВремениПриИзменении(Элемент)
КонецПроцедуры

&НаКлиенте
Процедура ЧСД_ПриоритетЗаявкиПриИзмененииПосле(Элемент)
	// Провести новый расчет планового времени закрытия
	ЧСД_РасчетПлановогоВремениПриИзменении(Элемент)
КонецПроцедуры

&НаКлиенте
Процедура ЧСД_Ч_ТипЗаявкиПриИзмененииПосле(Элемент)
	// Провести новый расчет планового времени закрытия
	ЧСД_РасчетПлановогоВремениПриИзменении(Элемент)
КонецПроцедуры

&НаКлиенте
Процедура ЧСД_ПисьмоПосле(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ч_Предмет) Тогда
		
		стрПараметры = Новый Структура("Ключ", Объект.Ч_Предмет);
		ФормаОснования = ПолучитьФорму("Документ.ВходящееПисьмо.ФормаОбъекта", стрПараметры);
		
		Если  ФормаОснования <> Неопределено Тогда
			ФормаОснования.Открыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЧСД_ПроцессыИЗадачиПосле(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Предмет", Объект.Ссылка);
	ПараметрыФормы.Вставить(
		"Заголовок",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процессы и задачи по предмету: ""%1""'"),
			Заголовок));
	ОткрытьФорму(
		"ОбщаяФорма.ПроцессыИЗадачи",
		ПараметрыФормы);

КонецПроцедуры
